1000 PRINT "E5 - Clear out unused sectors by writing a big file full of 0xE5's"
1010 D$="B"
1020 PRINT "Drive <";D$;"> or Q to quit";
1030 INPUT I$
1040 IF I$="" THEN I$=D$
1050 IF LEN(I$) > 1 THEN GOTO 1020
1060 IF I$ > CHR$(96) THEN I$=CHR$(ASC(I$)-32)
1070 IF I$="Q" THEN GOTO 9000
1075 IF I$="X" THEN GOTO 9999
1080 IF (I$ < "A") OR (I$ > "P") THEN GOTO 1020
1090 F$=I$+":E5.$$$"
1100 OPEN "O",#1,F$
1110 E5$=CHR$(229)
1120 SECT$=""
1130 PRINT "Number of Kb  ";: INPUT K
1140 FOR I=1 TO 128
1150 SECT$=SECT$+E5$
1160 NEXT I
1165 ON ERROR GOTO 9100
1170 FOR J=1 TO K
1180 FOR I=1 TO 8
1190 PRINT #1,SECT$;
1200 NEXT I
1210 NEXT J
1220 CLOSE #1
1230 KILL F$
9000 SYSTEM
9100 IF (ERR=61) AND (ERL=1190) THEN RESUME 1220
9110 PRINT "Unexpected error ERR=";ERR;", ERL=";ERL
9999 END
